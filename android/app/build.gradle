apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "org.mozilla.sendandroid"
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions "abi"

    productFlavors {
        x86 { dimension "abi" }
        x86_64 { dimension "abi" }
        arm { dimension "abi" }
        aarch64 { dimension "abi" }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha02'
    implementation "org.mozilla.components:service-firefox-accounts:${rootProject.ext.fxa_component_version}"
    implementation "org.mozilla.components:browser-engine-gecko-beta:${rootProject.ext.android_components_version}"
    implementation "org.mozilla.components:concept-engine:${rootProject.ext.android_components_version}"
    implementation "org.mozilla.components:feature-session:${rootProject.ext.android_components_version}"
    implementation "org.mozilla.components:browser-session:${rootProject.ext.android_components_version}"
    x86Implementation     "org.mozilla.geckoview:geckoview-${rootProject.ext.geckoviewChannel}-x86:${rootProject.ext.geckoviewVersion}"
    x86_64Implementation  "org.mozilla.geckoview:geckoview-${rootProject.ext.geckoviewChannel}-x86_64:${rootProject.ext.geckoviewVersion}"
    armImplementation     "org.mozilla.geckoview:geckoview-${rootProject.ext.geckoviewChannel}-armeabi-v7a:${rootProject.ext.geckoviewVersion}"
    aarch64Implementation "org.mozilla.geckoview:geckoview-${rootProject.ext.geckoviewChannel}-arm64-v8a:${rootProject.ext.geckoviewVersion}"
}

task generateAndLinkBundle(type: Exec, description: 'Generate the android.js bundle and link it into the assets directory') {
    commandLine './buildAssets.sh'
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn generateAndLinkBundle
}
